load("//:config.bzl", "assembly_version", "author", "version")
load("//tools/build:csharp.bzl", "csharp_assembly_info", "csharp_library", "csharp_nunit_test")
load("//tools/build/mono-4.5:build.bzl", "mono_net_libs")
load("//tools/ServiceDefinitions:build.bzl", "service_definitions")

filegroup(
    name = "core",
    srcs = [
        ":KRPC.Core",
        ":ServiceDefinitions",
    ],
    visibility = ["//:__pkg__"],
)

test_suite(
    name = "test",
    tests = [
        ":test-KRPC.Core",
    ],
)

genrule(
    name = "version",
    outs = ["VERSION.txt"],
    cmd = 'echo "%s" > "$@"' % version,
)

csharp_assembly_info(
    name = "AssemblyInfo",
    cls_compliant = False,
    copyright = author,
    description = "Core server functionality for kRPC",
    internals_visible_to = [
        "KRPC.Core.Test",
        "DynamicProxyGenAssembly2",
    ],
    title = "KRPC.Core",
    version = assembly_version,
    visibility = [
        "//:__pkg__",  # Make visible to //:csproj-deps so it can copy AssemblyInfo.cs to generated_deps
        "//tools/ServiceDefinitions:__pkg__",  # Make visible to ServiceDefinitions so it can build KRPC.Core.dll with .NET 4.5 references
    ],
)

# Make sources visible to ServiceDefinitions so it can build KRPC.Core.dll with .NET 4.5 references
filegroup(
    name = "KRPC.Core-src",
    srcs = glob(["src/**/*.cs"]),
    visibility = ["//tools/ServiceDefinitions:__pkg__"],
)

srcs = glob(["src/**/*.cs"]) + [
    ":AssemblyInfo",
    "//protobuf:csharp_unity",
]

deps = [
    "//tools/build/ksp:Google.Protobuf",
    "//tools/build/ksp:KRPC.IO.Ports",
] + mono_net_libs

csharp_library(
    name = "KRPC.Core",
    srcs = srcs,
    visibility = ["//visibility:public"],
    deps = deps,
)

service_definitions(
    name = "ServiceDefinitions",
    out = "KRPC.Core.json",
    assemblies = [":KRPC.Core"],
    service = "KRPC",
    visibility = ["//visibility:public"],
)

csharp_assembly_info(
    name = "TestAssemblyInfo",
    cls_compliant = False,
    copyright = author,
    title = "KRPC.Core.Test",
    version = assembly_version,
    visibility = [
        "//:__pkg__",  # Make visible to krpc-genfiles so it can copy AssemblyInfo.cs to zip
    ],
)

test_deps = [
    ":KRPC.Core",
    "//tools/build/mono-4.5:Moq",
    "//tools/build/ksp:Google.Protobuf",
    "//tools/build/ksp:KRPC.IO.Ports",
    "//tools/build/mono-4.5:Newtonsoft.Json",
    "//tools/build/mono-4.5:System.Threading.Tasks.Extensions",
    "//tools/build/mono-4.5:Castle.Core",
] + mono_net_libs

csharp_library(
    name = "KRPC.Core.Test",
    srcs = glob(["test/**/*.cs"]) + [":TestAssemblyInfo"],
    define = ["CODE_ANALYSIS"],
    nowarn = [
        "1591",
        "1685",
    ],
    nunit_test = True,
    optimize = False,
    visibility = ["//:__pkg__"],
    deps = test_deps,
)

csharp_nunit_test(
    name = "test-KRPC.Core",
    size = "small",
    lib = ":KRPC.Core.Test",
    # FIXME: need to run test outside the sandbox to work around nunit file accesses
    tags = ["local"],
    deps = test_deps,
)
